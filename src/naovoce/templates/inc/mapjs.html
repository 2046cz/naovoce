{% load i18n %}
<script type="text/coffeescript" charset="utf-8">

geocodeService = new L.esri.Geocoding.geocodeService
Naovoce.CENTER_CS = new L.LatLng 49.829896, 15.514818


class MarkerIcon extends L.DivIcon
	constructor: (color, char) ->
		super
			className: 'marker',
			iconSize: null,
			html: "<span style='color:##{ color }'>&#x#{ char }</span>"


KINDS =
	'F00D':
		icon: new MarkerIcon('555', 'F00D')
		name: "{% trans 'Fruit kind' %}"
	{% for k in kinds %}
	'{{ k.key }}':
		icon: new MarkerIcon('{{ k.color }}', '{{ k.key }}')
		name: '{{ k.name }}'
	{% endfor %}


getDMSCoords = (lat, lng) ->
	DD2DMS = (dd) ->
		d: 0 | (dd = Math.abs(dd))
		m: 0 | dd % 1 * 60
		s: (0 | dd * 60 % 1 * 6000) / 100

	fmtDMS = (dms) ->
		"#{dms.d}&deg;#{dms.m}&prime;#{dms.s}&Prime;"

	"#{fmtDMS DD2DMS(lat)}#{if lat<0 then 'S' else 'N'}&nbsp;" +
	"#{fmtDMS DD2DMS(lng)}#{if lng<0 then 'W' else 'E'}"


class Naovoce.Marker extends L.Marker
	constructor: (latlng, @kind) ->
		super latlng, icon: KINDS[@kind].icon

	setIcon: (kind) ->
		super KINDS[kind].icon


class FruitPopup extends L.Popup
	constructor: (fruit, options) ->
		super options
		@_header = "<h4><a href='/fruit/detail/#{ fruit.id }/'>#{ KINDS[fruit.kind].name }" +
			"<br><small>(#{getDMSCoords fruit.lat, fruit.lng})</small></a></h4>"

	setContent: (content) ->
		@_content = @_header + content
		@.update()
		@


class PopupMarker extends Naovoce.Marker
	constructor: (fruit) ->
		@fruit = fruit
		super [fruit.lat, fruit.lng], fruit.kind

		@bindPopup new FruitPopup fruit,
			offset: new L.Point(0, -26)
			maxWidth: 200


class MarkersDB
	###
	Browser's offline markers cache, built around Naovoce.db promise object (indexedDB)
	###
	constructor: ->
		Naovoce.db.fail (msg) ->
			console.log msg

	fetchAll: (callback) ->
		Naovoce.db.done (db) =>
			trans = @transaction db
			store = @objectStore trans
			items = []

			trans.oncomplete = (event) =>
				timestamp = @getTimestamp() or @now()
				callback items, timestamp
			trans.onerror = ->
				callback()

			cursorRequest = store.openCursor()
			cursorRequest.onsuccess = (event) ->
				cursor = event.target.result
				if cursor
					items.push cursor.value
					cursor.continue()
			cursorRequest.onerror = ->
				callback()

		Naovoce.db.fail (msg) ->
			callback()

	clearAll: ->
		Naovoce.db.done (db) =>
			@setTimestamp @now()

			@objectStore(@transaction db)
				.clear()

	resetAll: (data) ->
		Naovoce.db.done (db) =>
			@setTimestamp @now()

			store = @objectStore(@transaction db)

			store.clear()
			for item in data
				store.put item

	update: (data) ->
		Naovoce.db.done (db) =>
			@setTimestamp @now()

			store = @objectStore(@transaction db)

			for item in data.deleted
				store.delete item.id
			for item in data.created
				store.put item
			for item in data.updated
				store.put item

	{# helper functions #}
	transaction: (db) ->
		db.transaction 'markers', 'readwrite'

	objectStore: (trans) ->
		trans.objectStore 'markers'

	setTimestamp: (time) ->
		Naovoce.storage.setObject 'markersUpdated', time

	getTimestamp: ->
		Naovoce.storage.getObject 'markersUpdated'

	now: ->
		d = new Date()
		pad = (n) -> if n < 10 then '0'+n else n
		"#{d.getFullYear()}-#{pad(d.getUTCMonth()+1)}-#{pad(d.getUTCDate())}/" +
		"#{pad(d.getUTCHours())}:#{pad(d.getUTCMinutes())}:#{pad(d.getUTCSeconds())}/"


class Naovoce.Map extends L.Map
	###
	Map class used across the project. Extends L.Map by
	###
	constructor: (id) ->
		params =
			maxZoom: 18,
			minZoom: 5,

		super id,
			layers: [L.tileLayer('http://{s}.tile.osm.org/{z}/{x}/{y}.png', params)]

		{# Do not display attributions, they are in the footer. #}
		@attributionControl.setPrefix ''

		if L.markerClusterGroup?
			@markerLayerGroup = L.markerClusterGroup
				chunkedLoading: true
				spiderfyOnMaxZoom: false
				showCoverageOnHover: false
				removeOutsideVisibleBounds: true
				disableClusteringAtZoom: 15
				maxClusterRadius: 40
			.addTo @
		else
			@markerLayerGroup = new L.layerGroup().addTo @
			@markerLayerGroup.addLayers = (layers) ->
				@addLayer l for l in layers

		@markersDB = new MarkersDB()

		{# Add more content to popups when marker clicked. #}
		@.on 'popupopen', (ev) ->
			$.getJSON ev.popup._source.fruit.url, (json) =>
				content = "<p><strong>{% trans 'Shared by' %}:</strong> " +
					"<a href='/pickers/#{json.user.id}-#{json.user.username.toLowerCase()}/'>"+
					"#{json.user.username} &raquo;</a></p>"

				content += "<p><strong>{% trans 'Description' %}:</strong> " +
					"#{json.description.truncate(150)}<p>" if json.description

				ev.popup.setContent content

				@fetchAddress [json.lat, json.lng], (address) ->
					ev.popup.setContent "<p><strong>{% trans 'Address' %}:</strong> " +
						"#{address}</p>" + content if address
			@

		{# Add spinner DIV into map container. #}
		@spinner = $ '<div id="spinner">'
		$("##{id}").prepend @spinner

	loadMarkers: (items) ->
		@spinner.addClass 'spinning'

		if items?.length
			@markers = (new PopupMarker fruit for fruit in items)
			@displayMarkers()
			@spinner.removeClass 'spinning'
		else
			{# No items? Try to load them from local DB. #}
			@markersDB.fetchAll (items, lastFetched) =>
				{# Did callback receive local data? #}
				if items?.length
					{# Fetch only diff since timestamp. #}
					$.getJSON "{% url 'api:fruit-diff' %}#{lastFetched}", (diff) =>
						@markersDB.update diff
						@markersDB.fetchAll (items) =>
							@loadMarkers items
				else
					{# Else fetch data remotely and try to save them locally. #}
					$.getJSON "{% url 'api:fruit-list' %}", (items) =>
						@loadMarkers items
						@markersDB.resetAll items
		@

	displayMarkers: ->
		@markerLayerGroup.clearLayers()
		@markerLayerGroup.addLayers @markers
		@

	filterMarkers: (kind) ->
		@markerLayerGroup.clearLayers()
		@markerLayerGroup.addLayers (m for m in @markers when m.kind == kind)
		@

	clearMarkers: ->
		@markerLayerGroup.clearLayers()
		@

	{# functions to share map state between page reloads #}
	saveState: ->
		center = @getCenter()
		Naovoce.storage.setObject 'map',
			pos: [center.lat, center.lng]
			zoom: @getZoom()
		@

	loadState: ->
		map = Naovoce.storage.getObject 'map'

		if map?
			@setView map.pos, map.zoom
		else
			@setView Naovoce.CENTER_CS, 8
		@

	focusTo: (latlng) ->
		@setView latlng
		@setZoom 10
		@

	fetchAddress: (latlng, onSuccess) ->
		geocodeService.reverse()
			.latlng latlng
			.run (error, result) ->
				if result
					loc = result.address
					address = "#{ loc.City }, #{ loc.CountryCode }"
					if loc.Postal
						address = "#{ loc.Postal } #{ address }"
					if loc.Address
						address = "#{ loc.Address }, #{ address }"
					onSuccess address
		@

	addSearch: ->
		arcgisOnline = L.esri.Geocoding.arcgisOnlineProvider()
		searchControl = new L.esri.Geocoding.geosearch
			placeholder: "{% trans 'Search for places or addresses' %}"
			title: "{% trans 'Location search' %}"
			providers: [arcgisOnline]
			useMapBounds: false

		{# Do not display attributions, they are in the footer. #}
		searchControl.getAttribution = -> ''
		searchControl.addTo @

		results = new L.LayerGroup().addTo @

		searchControl.on 'results', -> results.clearLayers()
		@

	addLocate: ->
		L.control.locate
			icon: 'locator',
			iconLoading: 'locator busy'
			strings:
				title: "{% trans 'Show me where I am' %}"
				metersUnit: "m"
				popup: "{% trans 'You are within {distance} {unit} from this point' %}"
		.addTo @
		@

</script>
